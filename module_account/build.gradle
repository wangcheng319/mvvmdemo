//apply plugin: 'com.android.application'

if (isModule.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


def app = rootProject.ext.android
def config = rootProject.ext.app
def dep = rootProject.ext.dep

android {
    compileSdkVersion app.compileSdkVersion
    defaultConfig {
        //如果是独立模块，则使用当前组件的包名
        if (!isModule.toBoolean()) {
            applicationId "com.dajiabao.module_account"
        }
        minSdkVersion app.minSdkVersion
        targetSdkVersion app.targetSdkVersion
        versionCode config.versionCode
        versionName config.versionName

        //ARouter
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

        resourcePrefix "account_"
    }

    //配置资源文件
    sourceSets {
        main {
            if (isModule.toBoolean()) {//如果moudle为组件则配置 AndroidManifest
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
                // 配置组件模式下的java代码主文件
                java.srcDirs 'src/main/java', 'src/main/module/java'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'


    implementation project(path: ':common')
    //arouter
    api 'com.alibaba:arouter-api:1.5.0'
    kapt 'com.alibaba:arouter-compiler:1.2.2'

}
